CREATE FUNCTION [dbo].[COMPUTE_PRORATA_MONTANT] 
(
	@DT_DEBUT   DATE,
	@DT_FIN     DATE,
	@MT_ANNUEL  NUMERIC(13,2),
	@FRQ        INT
)
RETURNS NUMERIC(13,2)
AS
BEGIN


--RETOURNER 0 SI LA DUREE EST SUPERIEUR A 20 AND ET SI LA FREQUENCE N'EST PAS 1, 3, 6 OU 12
IF @FRQ NOT IN (1, 3, 6, 12) OR ISNULL(DATEDIFF(year, @DT_DEBUT, @DT_FIN), 999) > 20
	RETURN 0


DECLARE @DEBUT_TERME DATE, @FIN_TERME DATE, @MT NUMERIC(13,2)
SET @DEBUT_TERME = DATEADD(month, -(MONTH(@DT_DEBUT) - 1) % @FRQ, DATEADD(day, -DAY(@DT_DEBUT) + 1, @DT_DEBUT))
SET @MT = 0

WHILE @DEBUT_TERME <= @DT_FIN
	BEGIN
	SET @FIN_TERME = DATEADD(day,  -1,  DATEADD(month, @FRQ, @DEBUT_TERME))
	
	IF @DT_DEBUT <= @DEBUT_TERME AND @FIN_TERME <= @DT_FIN
		BEGIN
		--TERME ENTIER
		SET @MT = @MT + @MT_ANNUEL * @FRQ / 12
		END
    ELSE
		BEGIN
		--TERME PARTIEL
		SET @MT = @MT
		          +
		          @MT_ANNUEL * @FRQ / 12
		          *
		          (DATEDIFF(day,
		                    CASE WHEN @DT_DEBUT > @DEBUT_TERME THEN @DT_DEBUT ELSE @DEBUT_TERME END,
		                    CASE WHEN @DT_FIN   < @FIN_TERME   THEN @DT_FIN   ELSE @FIN_TERME END) + 1)
		          /
		          (DATEDIFF(day, @DEBUT_TERME, @FIN_TERME) + 1)
		END
    
	SET @DEBUT_TERME = DATEADD(month, @FRQ, @DEBUT_TERME)
	END


RETURN @MT

END